plugins {
    id "net.minecraftforge.gradle.forge" version "6f53277"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id 'org.spongepowered.mixin' version "d5f9873"
    id "java"
}

version = projectVersion
group = projectGroup
archivesBaseName = projectName

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    replace("@NAME@", projectName)
    replace("@VER@": projectVersion)
    replace("@ID@", projectId)
    clientRunArgs += "--mixin mixins.tabstats.json"
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io/' }
    maven { url "https://repo.sk1er.club/repository/maven-public/" }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
    implementation("gg.essential:vigilance-1.8.9-forge:${vigilanceVersion}")
    compile 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
}

processResources {
    inputs.property("version", version)
    inputs.property("mcversion", minecraft.version)

    filesMatching("mcmod.info") {
        expand(
                "id": projectId,
                "name": projectName,
                "version": version,
                "mcversion": minecraft.version
        )
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task moveResources {
    doLast {
        try {
            ant.move(
                    file: "${buildDir}/resources/main",
                    todir: "${buildDir}/classes/java"
            )
        } catch (Exception e) {
            e.printStackTrace();
        }
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources

jar {
    manifest.attributes(
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.tabstats.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
    )

    enabled = false
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.tabstats.refmap.json"
}

shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    archiveClassifier.set("")
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}


sourceSets {
    main {
        ext.refMap = "mixins.tabstats.refmap.json"
    }
}

tasks.reobfJar.dependsOn tasks.shadowJar
moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)
jar.finalizedBy(shadowJar)