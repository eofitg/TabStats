// ============= Use buildscript & apply method for ForgeGradle 2.1  =============
buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        maven { url = "https://jitpack.io" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

// ============= Project Info =============
version = projectVersion
group = projectGroup
archivesBaseName = projectName

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

// ============= Forge Config =============
minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    replace "@NAME@", projectName
    replace "@VER@", projectVersion
    replace "@ID@", projectId
    clientRunArgs += "--mixin mixins.tabstats.json"
}

// ============= Resource =============
processResources {
    inputs.property("version", version)
    inputs.property("mcversion", minecraft.version)

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand(
                id: projectId,
                name: projectName,
                version: version,
                mcversion: minecraft.version
        )
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// ============= Resources Movement Task =============
task moveResources {
    doLast {
        try {
            ant.move(
                    file: "${buildDir}/resources/main",
                    todir: "${buildDir}/classes/java"
            )
        } catch (Exception e) {
            e.printStackTrace()
        }
    }
}

moveResources.dependsOn processResources
classes.dependsOn moveResources

// ============= Jar Config =============
jar {
    manifest.attributes(
            "ForceLoadAsMod": true,
            "MixinConfigs": "mixins.tabstats.json",
            "ModSide": "CLIENT",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
    )
    enabled = false
}

// ============= Mixin Config =============
mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.tabstats.refmap.json"
}

sourceSets {
    main {
        ext.refMap = "mixins.tabstats.refmap.json"
    }
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

// ============= Repo & Dep =============
repositories {
    mavenCentral()
    maven { url = "https://files.minecraftforge.net/maven" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    shade 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
}

// ============= ShadowJar Config =============
shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    classifier = ""
}

// ============= Reobf Config =============
reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

tasks.reobfJar.dependsOn tasks.shadowJar
